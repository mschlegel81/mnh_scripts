USE GUI;
curry tracepoint(radius,t)->begin
  local phi:=(t*(1/radius-1))+pi/2;
  [sin(phi),cos(phi)]*radius+(1-radius)*[cos(t),sin(t)];
end;

getTrace(radius,range)->([0..150]/150*range).map(tracepoint(radius));

//S:=sin([0..50]/50*2*pi);
//C:=cos([0..50]/50*2*pi);
drawInner(radius,t,color)->begin
  local phi:=(  t*(  1/radius-1))+pi/2;
  local center:=(1-radius)*[cos(t),sin(t)];
  addPlot([center[0],center[0]+sin(phi)*radius],[center[1],center[1]+cos(phi)*radius],'. l '&color);
  addPlot([center-radius,center+radius],'ellipse '&color);
end;

main->main('3/7');
main(innerRadius)->begin
  freeConsole;
  setOptions([['x0',-1],['x1',1],
              ['y0',-1],['y1',1],
              ['autoscaleX',false],
              ['autoscaleY',false],
              ['axisStyleX',0],
              ['axisStyleY',0]]);
  clearAnimation;
  local r0:=interpret(innerRadius);
  r0.isNumeric AND 0<r0<1 ? void : fail('Invalid radius: ',innerRadius);
  local f:=1-1/r0;
  local period:=begin
    local p0:=tracepoint(r0,0);
    ([1..100]*2*pi).each(t,[t,(tracepoint(r0,t)-p0).sqr.agg(+).sqrt*t]).sort(1).head.head;
end;

getTrace(r0,period).plot;
addPlot([[-1,-1],[1,1]],'black ellipse');
([0..299]/300*period).each(t,begin
  drawInner(r0,t,'red');
  drawInner(1-r0,f*t,'blue');
  addAnimationFrame;
  removePlot(4); end);
end;
