D(x:NumericList(3))->[10*(x[1]-x[0]),
                      28*x[0]-x[1]-x[0]*x[2],
                      x[0]*x[1]-8/3*x[2]];

USE rungeKutta,plot3d;


heightOfTriangle(A:NumericList(3),
                 B:NumericList(3),
                 C:NumericList(3))->begin
  X:=C-A;
  Y:=B-A;
  euklideanNorm(X*((X*Y).agg(+)/(sqr(X).agg(+)))-Y);
end;

filterList(L:List)->begin
  result:=[x:=L[0]];
  buffer:=[];
  L.tail.each(y,
    buffer.each(b,heightOfTriangle(x,b,y)<0.03,AND)
    ? buffer|=[y]
    : begin
        result|=[x:=buffer.trailing];
        buffer:=[y];
        void;
      end);
  buffer.size>0
  ? result|=[buffer.trailing]
  : void;
  print('filtering reduced list size from ',L.size,' to ',result.size);
  result;
end;

showSingleAttractor->begin
  step:=integrator(CASH_KARP,::D,1E-2,1E-3);
  L:=[1,0,30].iterate(step,20000).tail(200);//.filterList;
  c:=L.boundingSphere[0];
  clearAnimation;
  print(repeatString('-'.repeatString(24)&'|',4));
  ([0..99]/100*2*pi).toIterableExpression.pMap({
    projectOnly(L.rotateX(pi/2).rotateY($angle),
              [c].rotateX(pi/2).rotateY($angle).head,0.5)})
  .each(row,begin
    printDirect('.');
    setOptions(['axisStyleX'=>0,
                'axisStyleY'=>0,
                'autoscaleX'=>false,
                'autoscaleY'=>false,
                'x0'=>-1,'x1'=>1,'y0'=>-1,'y1'=>1]);
    row.plot('RGB0,0,0,0.5 0.7 b');
    addAnimationFrame;
  end);
  print;
end;

showAnimation->
begin
  step:=integrator(CASH_KARP,::D,1E-2,1E-3);
  clearAnimation(true);
  resetOptions;
  y0:=x0:= Inf;
  y1:=    -Inf;

  state0:=[1,0,30+0   ];
  state1:=[1,0,30+1E-3];
  hist0:=[]; fHist0:=[];
  hist1:=[]; fHist1:=[];
  postDisplay;
  while true do begin
    hist0|=[state0]; state0:=state0.step;
    hist1|=[state1]; state1:=state1.step;
    t0:=scriptTime;
    hist0.size>1000
    ? begin
        fHist0:=filterList(fHist0|hist0); hist0:=[hist0.trailing];
        fHist1:=filterList(fHist1|hist1); hist1:=[hist1.trailing];
      end
    : void;
    setOptions(['axisStyleX'=>0,
                'axisStyleY'=>0,
                'x0'=>x0:=min(x0,-euklideanNorm(state0[[0,1]])),
                'x1'=>-x0,
                'y0'=>y0:=min(y0,state0[2]),
                'y1'=>y1:=max(y1,state0[2])]);
    plot();
    fHist0.size>0
    ? begin
        addPlot(fHist0.getInner(0),fHist0.getInner(2),'red l');
        addPlot(fHist1.getInner(0),fHist1.getInner(2),'blue l');
      end
    : void;
    hist0.size>0
    ? begin
        addPlot(hist0.getInner(0),hist0.getInner(2),'red l');
        addPlot(hist1.getInner(0),hist1.getInner(2),'blue l');
      end
    : void;
    addPlot([state0[[0,2]]],'red .');
    addPlot([state1[[0,2]]],'blue .');
    addAnimationFrame;
    printf('%5d %3.5fs',size(hist0),scriptTime-t0);
  end;
end;

showDots->begin
  step:={begin d:=D($x); $x+d/d.euklideanNorm*0.5; end};
  L:=//[random,random,random].iterate(step,5000).tail(500);//.filterList;
  [0..31].pEach(run,[-10+random,-15+random,24+random].iterate(step,5000).tail(4000),|);
  print(L.size,' dots initial');
                   L:=group(L,round(2*L)).map({$x[1].agg(+)/$x[1].size}); print(L.size,' dots after grouping A');
  L|=L.pMap(step); L:=group(L,round(2*L)).map({$x[1].agg(+)/$x[1].size}); print(L.size,' dots after grouping B');
  L|=L.pMap(step); L:=group(L,round(2*L)).map({$x[1].agg(+)/$x[1].size}); print(L.size,' dots after grouping C');
  L|=L.pMap(step); L:=group(L,round(2*L)).map({$x[1].agg(+)/$x[1].size}); print(L.size,' dots after grouping D');
  L|=L.pMap(step); L:=group(L,round(2*L)).map({$x[1].agg(+)/$x[1].size}); print(L.size,' dots after grouping E');
  c:=L.boundingSphere[0];
  clearAnimation;
  print(repeatString('-'.repeatString(24)&'|',4));
  ([0..99]/100*2*pi).toIterableExpression.pMap({
    projectOnly(L.rotateX(pi/2).rotateY($angle),
              [c].rotateX(pi/2).rotateY($angle).head,0.5)})
  .each(row,begin
    printDirect('.');
    setOptions(['axisStyleX'=>0,
                'axisStyleY'=>0,
                'autoscaleX'=>false,
                'autoscaleY'=>false,
                'x0'=>-1,'x1'=>1,'y0'=>-1,'y1'=>1]);
    row.plot('0.5 . RGB0,0,0,0.5');
    addAnimationFrame;
  end);
  print;
end;


main->showSingleAttractor;
main('dots')->showDots;
main('anim')->showAnimation;


