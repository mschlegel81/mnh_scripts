#!C:\bin\mnh.exe -quiet -silent -convertPrintToLog -logDateFmt hh:mm:ss -logLocationLength 15 +log stdOut(2)
datastore scanDiameter:=2;
datastore best:=[];
mutable previouslySeenBaseForms:=[].toSet;

T:=[-3..-1,1..3].map((n)->n^[0..2]);

nextCoefficientCandidates->begin
  scanDiameter+=1;
  primes(scanDiameter-1)
  .each(p,transpose([[p],
          [0..scanDiameter-p-1],
          [scanDiameter-p..1]],p),|)
  .each(x,x,x*[1,-1,1])
  .minus(previouslySeenBaseForms)
  .pFilter((q)->T.map((t)->isPrime((t*q).agg(+))).trueCount>=3)
  .toList;
end;

private longX:=[0..99999];
private longX2:=longX²;

longRunPrimes(a,b,c)->begin
  local p:=isPrime(a+b*longX+c*longX2);
  [p.head(100).trueCount,
   p.head(1000).trueCount,
   p.head(10000).trueCount,
   p.trueCount];
end;

POLYNOMIAL_FORMAT:='{$0}{$1<=0?"":"+"}{$1=-1 ? "-" : $1 in [0,1] ? "" : $1}{$1=0 ? "" : "n"}'&
                       '{$2<=0?"":"+"}{$2=-1 ? "-" : $2 in [0,1] ? "" : $2}{$2=0 ? "" : "n²"}';
PERCENTS_FORMAT:='%{$3/longX.size*100}5.3f%% (%{$2/longX.size*1000}4.2f%%/%{$1/longX.size*10000}3.1f%%/{$0}%%)';
formatResult(polynomial:IntList(3),succesivePrimes,longRunPrimes:IntList(4))->
  format("[%s]\t%s\tproduces\t%s\tprimes in a row and\t%s\tprimes in the long(er) run",
     polynomial.abs.agg(+),
     POLYNOMIAL_FORMAT.format@polynomial,
     succesivePrimes,
     PERCENTS_FORMAT.format@longRunPrimes);

calcCutoff(list:NumericList)->(list.agg(+)/list.size +
                               list.sort[list.size div 2])*0.5;//Average of arithmetic mean and median

successivePrimesInPolynomial       (a,b,c)->rangeGenerator(1).filter({!isPrime(a+$i*(b+$i*c))})();
successivePrimesInPolynomialReverse(a,b,c)->rangeGenerator(0,-10000).filter({!isPrime(a+$i*(b+$i*c))})();
shiftedForm(abc:IntList(3),k)->[abc[0]+abc[1]*k+abc[2]*k², abc[1]+2*abc[2]*k , abc[2]];

main->begin
  assertUniqueInstance;
  best.sort(1).map((q)->formatResult@q).join("\n").print;
  previouslySeenBaseForms:=best.pEach(q,[0..q[1]-1].each(k,shiftedForm(q[0],k)),|);
  print('-'.repeat(112));
  local basisForThresholds:=best.getInner(1);
  local threshold:=basisForThresholds.size=0 ? 0 : calcCutoff(basisForThresholds);
  while(true,begin
    printf('[{scanDiameter+1}] threshold = {threshold.ceil} ({threshold})');
    nextCoefficientCandidates
    .pMap((x)->begin
       local primesAhead:=successivePrimesInPolynomial@x;
       local indexShift :=successivePrimesInPolynomialReverse@x+1;
       [shiftedForm(x,indexShift),primesAhead-indexShift];
     end)
    .unique
    .filter((q)->begin
       q[1]>=threshold
       ? begin
           basisForThresholds|=q[1];
           threshold:=calcCutoff(basisForThresholds);
           true;
         end
       : false;
     end)
    .pMap((q)->[q[0],q[1],longRunPrimes@(q[0])])
    .agg((q)->begin
       best|=[q];
       print(formatResult@q);
       previouslySeenBaseForms|=[0..q[1]-1].each(k,shiftedForm(q[0],k));
     end);
     writeDataStores;
  end);
end;

main('list')->best.sort(1).each(q,print(formatResult@q));

primeHitRating(q)->begin
  local x:=[0..99999];
  x:=q[0,0]+x*(q[0,1]+x*q[0,2]);
  trueCount(x<=1)>0
  ? begin
      x:=x[x>1];
      round(q[2,3]/(1/ln(x)).agg(+)/x.size*100000,4);
    end
  : round(q[2,3]/(1/ln(x)).agg(+),4);
end;

basePolynomial(abc:IntList(3))->begin
  local k:=round(-abc[1] / (2*abc[2]));
  abc[1]+2*abc[2]*k < 0
  ? k+=sign(abc[2])
  : void;
  shiftedForm(abc,k);
end;


main('table')->begin
  assertGuiStarted;
  showTable(
  [['diam.','polynomial','succ','of 100','of 1000','of 10000','of 100000','Hit rating','Poly_B','n_0 for Poly_B']]|
  best.pMap((q)->q[0].abs.agg(+) | POLYNOMIAL_FORMAT.format@(q[0]) | q[1] | q[2] | primeHitRating(q) |
    begin
      local k:=round(-q[0,1] / (2*q[0,2]));
      q[0,1]+2*q[0,2]*k < 0
      ? k+=sign(q[0,2])
      : void;
      POLYNOMIAL_FORMAT.format@shiftedForm(q[0],k) | k;
    end


  //POLYNOMIAL_FORMAT.format@basePolynomial(q[0])

  ),'Prime generating polynomials',true);
end;


n:=[0..50];
7243-488*n+8*n²;
  43+ 88*n+8*n²;

basePolynomial([7243,-488,8]);
basePolynomial([  43,+ 88,8]);


