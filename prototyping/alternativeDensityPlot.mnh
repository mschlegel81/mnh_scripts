USE customFormUtil;
dp(F:Expression(2),x0,x1>x0,y0,y1>y0,sampleCount>=4,interactive:Boolean)->begin
  scale:=1/sqrt(sampleCount/((x1-x0)*(y1-y0)));

  width:=void;
  xy:=begin
    x:=x0+([0..(width:=round((x1-x0)/scale))-1]+0.5)*scale;
    y:=y0+([round((y1-y0)/scale)-1..0]+0.5)*scale;
    cross(y,x);
  end;
  f:=try(F,[xy.getInner(1),xy.getInner(0)],
                 {pMap(xy,(p)->F@p)});
  xy:=void;
  f0:=f.min;
  f1:=f.max;
  plot(); //clear plot
  f.pMap((z)->z<0 ? [1,0,0,z/f0] : [0,0,1,z/f1]).plotRasterImage(width,scale,x0,y0);

  interactive ? void : return void;

  display;
  x:=0;
  y:=0;
  f:=0;
  showDialog('Interactive Density Plot',[
    newLabel.withCaption({'x='&x}),
    newLabel.withCaption({'y='&y}),
    newLabel.withCaption({'f(x,y)='&f}),
    newPlotLink.withMouseMoved({begin
      x:=$x;
      y:=$y;
      f:=F($x,$y);
    end}),
    newPlotDock]);

end;

dp((x,y)->begin
     d:=euklideanNorm([x,y]);
     ((0.3*d>abs(x) ? 0.3*d-abs(x) : 0)-
      (0.3*d>abs(y) ? 0.3*d-abs(y) : 0))/d;
   end
,-20,20,-20,20,100000,true);
