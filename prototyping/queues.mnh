@SuppressUnusedParameterWarning
type Queue(E:StatefulExpression(2))->true;

newQueue->toQueue((method in ['put','putEnd','next','hasEnded'],value)->begin
  local queued:=[];
  local hasEndMarker:=false;
save;
  method=='put'    ? queued|=[value] :
  method=='putEnd' ? hasEndMarker:=true :
  method=='next'   ? begin
    local r:=queued[0];
    queued:=queued.tail;
    r;
  end : hasEndMarker;
end);

put   (Q:Queue,value)->Q('put'   ,value);
putEnd(Q:Queue)      ->Q('putEnd'    ,'');
next  (Q:Queue)      ->Q('next'      ,'');
hasEnd(Q:Queue)      ->Q('hasEnded'  ,'');

queueIterator(Q:Queue)->{begin
  local result:=void;
save;
  while((result:=Q.next).isVoid AND !Q.hasEnd,sleep(0.1));
  result;
end}.toIteratableExpression;

begin
  local queue:=newQueue;
  local iterator:=queueIterator(queue);

  localAsync({iterator.map(::factorize).agg((x)->print(x))});

  [1..100].map((i)->begin queue.put(i); sleep(0.05); end);
  queue.putEnd;
end;


