#!/usr/bin/mnh -GUI
arrow(shift:IntList(2),rotation:Numeric)->begin
  local u:=[ cos(rotation),sin(rotation)];
  local v:=[-sin(rotation),cos(rotation)];
  [[-1,-2],[-1,0],[-2,0],[0,2],[2,0],[1,0],[1,-2],[Nan,Nan]].map({$x[0]*u+$x[1]*v+shift});
end;

arrows(rot:Numeric,inverse:Boolean)->begin
  print('Drawing frame #',rot);
  inverse
  ? begin
      plot([[-25,-25],[25,25]],'black box fs');
      ([-6..5].cross([-6..5])*4+2).each(x,arrow(x,rot),|).addPlot('RGB1,1,1 fs 0 polygon');
    end
  : ([-5..5].cross([-5..5])*4).each(x,arrow(x,rot),|).plot('black fs 0 polygon');
end;

soft(i:Int)->(cos((1-(i mod 25)/25)*pi)+1)/2;

transit(i<25)->[      -pi/2*soft(i),false];
transit(i<50)->[  pi/2-pi/2*soft(i),true];
transit(i<75)->[  pi  -pi/2*soft(i),false];
transit(i   )->[3*pi/2-pi/2*soft(i),true];

//*Shows rotating arrows animation
main->begin
  setOptions(['x0'=>-10, 'x1'=>10, 'autoscaleX'=>false, 'axisStyleX'=>0, 'logscaleX'=>false,
              'y0'=>-10, 'y1'=>10, 'autoscaleY'=>false, 'axisStyleY'=>0, 'logscaleY'=>false,
              'preserveAspect'=>true].toMap);
  clearAnimation;
  [0..99].map(::transit).each(t,arrows@t,addAnimationFrame,index=0 ? display : void);
end;
