#!C:\bin\mnh.exe -GUI -quiet +out ?.log(1)
USE tinyServer_html,tinyServer_config,loggingRunner,serveTasks,cpuUsage;

@SuppressUnusedWarning
USE serveProcesses,serveFiles;

private LOG_NAME:=changeFileExt(myPath,'.log');
private uptimeFormat:='{floor($0/3600)}:%{floor($0/60) mod 60}2.2d:%{floor($0) mod 60}2.2d';

//--------------------------------------------------------------------------:CPU Usage
private getStatusSection->tag(
  ('Status').tag('h2')&htmlOuterTable(
  [['Date:'  .tag('b'),formatTime('dd.mm.yyyy',      systime),''                ,htmlLink('restart server','restart')],
   ['Time:'  .tag('b'),formatTime('hh:nn:ss',        systime),''                ,htmlLink('show server log','showLog')],
   ['Uptime:'.tag('b'),format(uptimeFormat,time)]])&getCpuDiagram,'div');

@SuppressUnusedParameterWarning
startPage(parameters:Map)->htmlPage('Server Overview',getHeadLinks&getStatusSection&getQueuedSection(true)&getHistorySection,true).wrapTextInHttp;

@SuppressUnusedParameterWarning
restartPage(parameters:Map)->begin
  enqueueTask(SPECIAL_TASK_RESTART,'');
  redirectTo(START_PAGE);
end;

@SuppressUnusedParameterWarning
ping(parameters:Map)->wrapTextInHttp('pong');

mutable serveStatistics:=[].toMap;

logServeStats->
  begin
    print('+---------------------------------- - - -');
    print('| Time ',formatTime(systime));
    print('| Uptime ',formatTime('hh:nn:ss',time/(24*60*60)));
    print('+---------------------------------- - - -');
    local list:=
    serveStatistics
      .map({$x[0]|$x[1]})
      .sort(2)
      .trailing(10);
    local totScale:=100/list.getInner(2).max;
    local avgScale:=100/(list.getInner(2)/list.getInner(1)).max;
    local timeLines:=[];

    list:=
    list.each(entry,
       begin
         timeLines|=[[entry[2]         ,'s (tot)',"#".repeat(round(entry[2]         *totScale))],
                     [entry[2]/entry[1],'s (avg)',"#".repeat(round(entry[2]/entry[1]*avgScale))]];
         format("\nRequest %s\n      x %s",entry[0],entry[1]);
       end);
    timeLines:=timeLines.map({$x.join("\t")}).join("\n").formatTabs;

    list.each(l,
      print(l),
      print(timeLines[2*index]),
      print(timeLines[2*index+1]));
  end;

PAGE_MAP:=[
  START_PAGE     => ::startPage,
  '/'            => ::startPage,
  ''             => ::startPage,
  '/restart'     => ::restartPage,
  '/ping'        => ::ping,
  '/favicon.ico' => ::favicon,
  '/showlog' =>
    {begin
       isMap($0);
       logServeStats;
       redirectTo(encodeRequest('','displayFile',['filename'=>LOG_NAME].toMap));
     end},
//File related:------------------------------
  '/displayfile'=>
    {begin
       local filename:=$p['filename'];
       (isString(filename) AND fileExists(filename)
        ? serveFile(filename)
        : void) orElse redirectTo(START_PAGE);
     end},
  '/downloadfile'=> ::downloadFile,
  '/files'       => ::serveFileList,
//Task related:------------------------------
  ENQUEUE_PAGE   => ::enqueuePage,
  '/kill'        => ::killTask,
  '/dequeue'     => ::dequeueTask,
  '/addtask'     => ::addTask,
  '/forcestart'  => ::forceStart,
  '/movetofront' => ::moveToFront,
//Process related:---------------------------
  '/processes/kill' => ::killProcess,
  '/processes'      => ::processesPage
  ].toMap;


serve(M)->begin
  local path:=M['request','path'];
  local parameters:=path.extractParameters;
        path      :=path.extractPath.lower;
  local serveTime:=-time;

  local serveRoutine:=PAGE_MAP[path];
  serveRoutine.isVoid
  ? tPrint('Unhandled path: ',path) orElse return redirectTo(START_PAGE)
  : void;
  local response:= //log info and redirect to start page
  try(serveRoutine,[parameters],
  {begin
     tPrint('Unhandled path: ',path);
     $messages.map({'    '&$x.join("\t")}).join("\n").print;
     redirectTo(START_PAGE);
   end});
  serveTime+=time;
  serveStatistics[path]:=(serveStatistics[path] orElse [0,0])+[1,serveTime];
  response orElse fail;
end;

main->begin
  assertUniqueInstance;
  local sinceLastTaskStart:=0;
  local restartScheduled:=false;
  startHttpServer(SERVE_AT_IP&':'&SERVE_AT_SOCKET,::serve,-1);
  loggingRunner.ensureServer;
  tPrint('server is up at ',SERVE_AT_IP&':'&SERVE_AT_SOCKET);

  while(!restartScheduled,begin
    logCpuUsage();
    nextTaskIsRestart
    ? restartScheduled:=true
    : (sinceLastTaskStart+=1)>=SAMPLES_FOR_WORKLOAD_CHECK AND isCpuAlmostIdle
      ? begin
          startExecution@getNextTask
          ? sinceLastTaskStart:=0
          : void;
          sleep(2);
        end
      : sleep(2);
  end);
  logServeStats;
end;

@after
restart->begin
  tPrint('Performing restart');
  local ipcIsUpAgain:=false;
  while(!ipcIsUpAgain,begin
    execAsync(executor,[myPath]);
    sleep(1);
    ipcIsUpAgain:=isLoggingRunnerIpcServerRunning;
  end);
end;
