USE processUtil,tinyServer_config;
private IPC_ID:=myPath.extractFileNameOnly;
private HEARTBEAT_INTERVAL:=1;
private niceInterval(timeInDays<0.00069444444444444447)->format('%0.3f',timeInDays*86400);
private niceInterval(timeInDays)->formatTime('hh:nn:ss.zzz',timeInDays);
private DATE_TIME_FORMAT:='yyyy-mm-dd hh:nn:ss.zzz';
private DATE_TIME_FORMAT_PATTERN:=DATE_TIME_FORMAT.replace(['a'..'z'],'\d').replace('.','\.').replace(':','\:').replace('-','\-');
private KILL_SIGNAL:=['KILL',197512];
private FINAL_LINE:='### - DONE - ###';


mutable SPECIAL_TASKS:=[].toMap;

FILESTATS       :='fileStats'       ;
SCRIPT          :='script'          ;
PARAMETERS      :='parameters'      ;
HEARTBEATTIMEOUT:='heartbeatTimeout';
RUNNING         :='running'         ;
KILLREQUESTED   :='killRequested'   ;
FINISHED        :='finished'        ;
STARTTIME       :='startTime'       ;
FINISHTIME      :='finishTime'      ;

datastore taskHistory:=[].toMap;

logTaskStarted(taskIndex:Int,scriptName:String,scriptParameters:String)->
  begin
    taskHistory[taskIndex]:=
    [FILESTATS       =>[],
     SCRIPT          =>scriptName,
     PARAMETERS      =>scriptParameters,
     HEARTBEATTIMEOUT=>scriptTime+HEARTBEAT_INTERVAL*3,
     RUNNING         =>true,
     FINISHED        =>false,
     STARTTIME       =>systime].toMap;
     log('Started task #',taskIndex,': ',scriptName,' ',scriptParameters.join(" "));
  end;

logSpecialTaskStarted(scriptName:String)->
  begin
    taskHistory[taskHistory.getInner(0).max+1]:=
    [FILESTATS       =>[],
     SCRIPT          =>scriptName,
     PARAMETERS      =>'',
     HEARTBEATTIMEOUT=>0,
     RUNNING         =>false,
     FINISHED        =>true,
     STARTTIME       =>systime].toMap;
  end;

setKillRequest(taskIndex)->taskHistory[taskIndex].isVoid ? void : begin
  log('Setting kill flag for task #',taskIndex,': ',taskHistory[taskIndex,SCRIPT],' ',taskHistory[taskIndex,PARAMETERS].join(" "));
  taskHistory[taskIndex,KILLREQUESTED]:=true;
end;

getTaskReport->
begin
  taskHistory
  .getInner(0)
  .each(taskIndex,begin
     pathToLog:=getLogName(taskIndex);
     current:=taskHistory[taskIndex];
     isSpecialTask:=current[SCRIPT] in SPECIAL_TASKS.getInner(0);
     running:=scriptTime<=current[HEARTBEATTIMEOUT];
     local fileStats:=fileInfo(pathToLog).getAll(['time','size']);
     fileStats==current[FILESTATS] AND running==current[RUNNING]
     ? void
     : begin
         lastLines:=fileExists(pathToLog) ? fileLineIterator(pathToLog).trailing(5) : [];
         finishTime:=parseTime(DATE_TIME_FORMAT, lastLines.filter({$line.matches('^\| Run finished at '&DATE_TIME_FORMAT_PATTERN)}).copy(18,DATE_TIME_FORMAT.length))[0];
         finishTime.isVoid
         ? fileExists(pathToLog)
           ? finishTime:=fileStats[0]
           : finishTime:=current[STARTTIME]
         : void;
         runIsFinished:=isSpecialTask OR (lastLines.trailing==FINAL_LINE);
         current[RUNNING   ]:=running AND !runIsFinished;
         current[FILESTATS ]:=fileStats;
         current[FINISHED  ]:=runIsFinished;
         log('-');
         current[FINISHTIME]:=isSpecialTask ? current[STARTTIME] : finishTime;
         log('-');
         runIsFinished
         ? current[HEARTBEATTIMEOUT]:=0
         : void;
         taskHistory[taskIndex]:=current;
       end;
   end);
  report:=taskHistory.map({[$x[1,RUNNING   ] orElse false,      //primary sort key
                            $x[1,FINISHTIME] orElse systime,    //secondary sort key
                            $x[1].mergeMaps(['index'=>$x[0]].toMap,::orElse)]}).sort.getInner(2).reverseList;
  while report.size>MAX_HISTORY_SIZE AND !report.trailing[RUNNING] do begin
    taskHistory[report.trailing['index']]:=void;
    report:=report.leading;
  end;
  report;
end;

isLoggingRunnerIpcServerRunning->IPC_ID.isIpcServerRunning;
ensureServer->isLoggingRunnerIpcServerRunning ? void :
  begin
    startIpcServer(IPC_ID,{postScriptRunning($X)});
    repeat sleep(0.01) until isLoggingRunnerIpcServerRunning;
  end;

private postScriptRunning(id:Int)->taskHistory[id].isVoid ? false : begin
  taskHistory[id,HEARTBEATTIMEOUT]:=scriptTime+3*HEARTBEAT_INTERVAL;
  taskHistory[id,RUNNING]:=true;
  (taskHistory[id,KILLREQUESTED] orElse false)
  ? KILL_SIGNAL
  : false;
end;

private splitCommandLineParameters(s:String)->
  regex.matchComposite(s,'("[^"]+"|[^\s"]+)').getInner(0).map({
    $x.copy(0,1)='"'=$x.copy($x.length-1,1)
    ? $x.copy(1,$x.length-2)
    : $x});

startExecution(scriptName,parameters:String)->begin
  logIndex:=taskHistory.getInner(0).max+1;
  logName:=getLogName(logIndex);
  programAndScript:=
    scriptName.extractFileExt.lower=='.bat'
    ? ['cmd','/C',scriptName]
    : scriptName.extractFileExt.lower=='.mnh'
    ? [executor,'-quiet','-headless','-convertPrintToLog','-logDateFmt','hh:mm:ss.zzz','-logLocationLength','25','+log','stdOut(1)',scriptName]
    : return void;
  logTaskStarted(logIndex,scriptName,parameters);
  writeDataStores;
  execAsync(executor,['-out',logName,myPath,logIndex.toString] | programAndScript | splitCommandLineParameters(parameters));
  ensureServer;
  true;
end;

@SuppressUnusedParameterWarning
startExecution(...)->false;

boxPrint(lines:List)->begin
  strings:=lines.map(::toString);
  len:=strings.map(::length).max;
  print(f"┌%{repeatString('─',len+2)}s┐\n",
        format("│ %{$0}-"&len&"s │",strings).join("\n"),
        f"\n└%{repeatString('─',len+2)}s┘\n");
end;

main(taskIndexAsString,program,...)->begin
  taskIndex:=taskIndexAsString.toInt;
  localAsync({begin
    killRequestReceived:=false;
    //We need the outer try for a graceful stop
    try({while not killRequestReceived do begin
           sleep(HEARTBEAT_INTERVAL);
           //We need the inner try for robustness if server dies
           try({sendIpcRequest(IPC_ID,taskIndex)})==KILL_SIGNAL
           ? killRequestReceived:=true
           : void;
         end}
       );
    killRequestReceived
    ? begin
        print('KILL SIGNAL RECEIVED - propagating');
        killAllMyChildren;
      end
    : void;
    end});
  boxPrint([join(program|...,' '),
            'Run started at '&formatTime(DATE_TIME_FORMAT,startTime:=systime)]);
  executionResult:=teeExec(program,...,::print);
  endTime:=systime;
  boxPrint(['Run finished at '&formatTime(DATE_TIME_FORMAT,endTime),
            'With exit code '&executionResult[1],
            'After '&niceInterval(endTime-startTime)]);
  print(FINAL_LINE);
end;

@SuppressUnusedWarning
private ON_STARTUP:=begin
  //On server startup, reset all timeouts
  taskHistory.getInner(0).each(taskIndex,
  taskHistory[taskIndex,HEARTBEATTIMEOUT]:=0);
  void;
end;
