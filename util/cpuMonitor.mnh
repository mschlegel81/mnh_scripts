cpuUsages->exec('wmic',['path','Win32_PerfFormattedData_PerfOS_Processor','get','Name,','PercentProcessorTime'])[0]
          .tail.filter({$x!=''}).map({$line.trim.replace('  ',' ').split(' ').softCast}).toMap;
cpuUsageTotal->cpuUsages['_Total'] orElse -1;

@SuppressUnusedWarning
waitForIdle(thresholdInPercent in [1..99])->while(cpuUsageTotal>thresholdInPercent,sleep(10));

niceBar(fill:Int)->niceBar(fill,-1);
niceBar(fill:Int,additionalTicAt:Int)->
  [0..100].each(k,k==additionalTicAt ? '#' :
                  k in [25,50,75,100]
                  ? k<=fill ? '+' : '|'
                  : k<=fill ? '-' : ' ').join;

main->while(true,print(formatTime('hh:mm.ss',systime),' ',niceBar(cpuUsageTotal)) orElse sleep(1));

//*Wait for cpu usage below <thresholdInPercent> and return
main(thresholdInPercent)->waitForIdle(thresholdInPercent.toInt);
